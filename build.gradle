
plugins {
    id "com.jfrog.bintray" version "1.8.0"
    id "maven-publish"
    id "java"
}

// Metadata.
group = 'com.teamten'
version = '1.0'

// In this section you declare where to find the dependencies of your project
repositories {
    // Use 'jcenter' for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
}

// In this section you declare the dependencies for your production and test code
dependencies {
    compile 'org.apache.pdfbox:pdfbox:2.0.4'
    compile 'com.google.guava:guava:20.0'
    compile 'com.teamten:teamten-util:1.1'
    compile 'com.teamten:teamten-image:1.0'
    compile 'org.jetbrains:annotations:16.0.2'

    testCompile 'junit:junit:4.12'
}

// My task for generating javadocs that are published on github IO pages.
task docs(type: Javadoc) {
    source = sourceSets.main.allJava
    destinationDir = file('docs')
    options {
        links "https://docs.oracle.com/javase/8/docs/api"

        // Noisy in git diffs:
        noTimestamp()
    }
}

// Add command-line options for compiler. Might not apply to tests.
tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked" << "-Werror"
}

// Where to put the jar file when the "uploadArchives" task is run.
uploadArchives {
    repositories {
        flatDir {
            dirs 'repo'
        }
    }
}

// https://reflectoring.io/guide-publishing-to-bintray-with-gradle/
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

// Used below in the "pom" object.
def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "lkesteloot"
            name "Lawrence Kesteloot"
            email "lk@teamten.com"
        }
    }

    scm {
        url "https://github.com/lkesteloot/teamten-typeset"
    }
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            groupId group
            artifactId 'teamten-typeset'
            version version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Book typesetting system in Java.')
                root.appendNode('name', 'Team Ten library')
                root.appendNode('url', 'https://github.com/lkesteloot/teamten-typeset')
                root.children().last() + pomConfig
            }
        }
    }
}

// To publish to bintray (jcenter).
bintray {
    user = System.getProperty('bintray.user')
    key = System.getProperty('bintray.key')
    publications = ['mavenPublication']

    pkg {
        repo = 'com.teamten'
        name = 'teamten-typeset'
        userOrg = ''
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/lkesteloot/teamten-typeset.git'
        version {
            name = '1.0'
            desc = '1.0'
            released = new Date()
        }
    }
}
