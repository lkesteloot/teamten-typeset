
plugins {
    id "application"
}

// Metadata.
group = 'com.teamten'
version = '1.0'

// In this section you declare where to find the dependencies of your project
repositories {
    // Use 'jcenter' for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
}

// In this section you declare the dependencies for your production and test code
dependencies {
    compile 'org.apache.pdfbox:pdfbox:2.0.4'
    compile 'com.google.guava:guava:20.0'
    compile 'com.teamten:teamten-util:1.1'
    compile 'com.teamten:teamten-image:1.0'
    compile 'org.jetbrains:annotations:16.0.2'

    testCompile 'junit:junit:4.12'
}

mainClassName = "com.teamten.typeset.Typesetter"

// My task for generating javadocs that are published on github IO pages.
task docs(type: Javadoc) {
    source = sourceSets.main.allJava
    destinationDir = file('docs')
    options {
        links "https://docs.oracle.com/javase/8/docs/api"

        // Noisy in git diffs:
        noTimestamp()
    }
}

// Add command-line options for compiler. Might not apply to tests.
tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked" << "-Werror"
}

task runx(dependsOn: classes, type: JavaExec) {
    main = project.hasProperty('mainClassName') ? project.mainClassName : ''
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in

    if (project.hasProperty('runWorkingDir')) {
        workingDir = new File(project.runWorkingDir)
    }
}

task printClasspath(dependsOn: classes) {
    println sourceSets.main.runtimeClasspath.asPath
}
